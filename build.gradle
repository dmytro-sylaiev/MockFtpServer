// MockFtpServer - Gradle build file

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

version = "2.8.0"
group = 'org.mockftpserver'
archivesBaseName = 'MockFtpServer'

sourceCompatibility = 1.4
targetCompatibility = 1.4

sourceSets {
    test {
        java { srcDirs = [] }
        groovy { srcDirs = ['src/test/java', 'src/test/groovy'] }
    }
}

repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
    jcenter()
}

dependencies {
    compile "org.slf4j:slf4j-api:1.7.30"

    testCompile "org.codehaus.groovy:groovy-all:1.8.9"
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'commons-net:commons-net:1.4.1'
    testCompile 'org.springframework:spring:2.5.6.SEC03'
    testCompile 'easymock:easymock:1.2_Java1.3'
    testCompile 'junit:junit:4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

//------------------------------------------------------------------------------
// Publish to Maven Central
//------------------------------------------------------------------------------

signing {
    required { gradle.taskGraph.hasTask(publish) }
    sign publishing.publications
}

// Publish to Sonatype (OSSRH) Maven Repository
// See http://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'MockFtpServer'
                description = 'The MockFtpServer project provides mock/dummy FTP server implementations for testing FTP client code.'
                url = 'https://mockftpserver.org'

                scm {
                    connection = 'scm:git:git@github.com:CodeNarc/CodeNarc.git'
                    developerConnection = 'scm:git:git@github.com:dx42/MockFtpServer.git'
                    url = 'scm:git:git@github.com:dx42/MockFtpServer.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'chrismair'
                        name = 'Chris Mair'
                        email = 'chrismair@users.sourceforge.net'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
