// MockFtpServer - Gradle build file

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

version = "3.0.0"
group = 'org.mockftpserver'
archivesBaseName = 'MockFtpServer'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    test {
        java { srcDirs = [] }
        groovy { srcDirs = ['src/test/java', 'src/test/groovy'] }
    }
}

repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
}

dependencies {
    compile "org.slf4j:slf4j-api:2.0.0"

    testCompile "org.codehaus.groovy:groovy-all:3.0.12"
    testCompile 'ch.qos.logback:logback-classic:1.3.0'
    testCompile 'commons-net:commons-net:3.8.0'
    testCompile 'org.springframework:spring:2.5.6.SEC03'
    testCompile 'org.mockito:mockito-core:3.9.0'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

//------------------------------------------------------------------------------
// Publish to Maven Central
//------------------------------------------------------------------------------

signing {
    required { gradle.taskGraph.hasTask(publish) }
    sign publishing.publications
}

// Publish to Sonatype (OSSRH) Maven Repository
// See http://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'MockFtpServer'
                description = 'The MockFtpServer project provides mock/dummy FTP server implementations for testing FTP client code.'
                url = 'https://mockftpserver.org'

                scm {
                    connection = 'scm:git:git@github.com:CodeNarc/CodeNarc.git'
                    developerConnection = 'scm:git:git@github.com:dx42/MockFtpServer.git'
                    url = 'scm:git:git@github.com:dx42/MockFtpServer.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'chrismair'
                        name = 'Chris Mair'
                        email = 'chrismair@users.sourceforge.net'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
